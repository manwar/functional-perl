#!/usr/bin/perl -w

use strict; use warnings FATAL => 'uninitialized';

# find modules from functional-perl working directory (not installed)
use Cwd 'abs_path';
our ($mydir, $myname); BEGIN {
    my $location= (-l $0) ? abs_path ($0) : $0;
    $location=~ /(.*?)([^\/]+?)_?\z/s or die "?";
    ($mydir, $myname)=($1,$2);
}
use lib "$mydir/../lib";

use Chj::xopen 'xopen_read';

my $f = xopen_read "$mydir/../MANIFEST";
our @module;
while (<$f>) {
    chomp;
    next unless s/\.pm$//;
    s|^lib/|| or die;
    s|/|::|sg;
    push @module, $_
}
$f->xclose;

# ------------------------------------------------------------------
# Non-core dependencies of modules to decide whether to skip testing
# them.  XX: keep these updated!

our %dependencies=
  (
   'Chj::FP::Text::CSV'=> [qw(Text::CSV)],
   'Chj::FP::url_'=> [qw(URI)],
  );


# ------------------------------------------------------------------


use Test::More;

for (@module) {
  SKIP: {
	if (my $ds= $dependencies{$_}) {
	    for my $d (@$ds) {
		eval "require $d; 1" or skip "don't have $d", 1
	    }
	}
	require_ok $_
    }
}

is( eval { Chj::TEST::run_tests() } // do { diag $@; undef}, 0, "run_tests");

done_testing;


#use Chj::ruse;
use Chj::Backtrace;
#use Chj::repl;
#repl;
